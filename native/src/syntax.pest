// Helper
escape = _{ "\\" ~ (!(NEWLINE | ASCII_HEX_DIGIT) ~ ANY | ASCII_HEX_DIGIT{1,6}) }
single_line_comment = _{ "//" ~ (!NEWLINE ~ ANY)* }
comma = _{ ws ~ "," ~ ws }
ident = _{ (ASCII_ALPHA | "-"{1,2} | "_" | escape) ~ (ASCII_ALPHANUMERIC | "-"{1,2} | "_" | escape)* }
ws = _{ WHITE_SPACE* }
nl = _{ ("\t" | " ")* ~ NEWLINE }

// Base expressions
symbol = { ident }
multi_line_comment = { "/*" ~ (!"*/" ~ ANY)* ~ "*/" }
function = { ident }
function_w = _{ function ~ "(" }
atrule = { ident }
atrule_w = { "@" ~ atrule }
hash = { ASCII_HEX_DIGIT{1,6} }
hash_w = _{ "#" ~ hash }
number = { 
	(
		("+" | "-")? ~
		(ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)? | "." ~ ASCII_DIGIT+)
	) ~
	(^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
}
single_string = { ("\\'" | "\\" ~ NEWLINE | !("'" | NEWLINE) ~ ANY)* }
double_string = { ("\\\"" | "\\" ~ NEWLINE | !("\"" | NEWLINE) ~ ANY)* }
string_w = _{
	("\"" ~ double_string ~ "\"")
	| ("'" ~ single_string ~ "'")
}
unit = { "%" | ASCII_ALPHA+ }
dimension = { number ~ unit }
var = { ident? }
var_w = _{ "$" ~ var }

// Composite expressions
surround = { (ASCII_ALPHANUMERIC | "." | "-" | "_" | "#" | "@" | escape)+ }
interpolation = {
	surround? ~
	(
		"{" ~ ws ~ expr ~ ws ~ "}" ~
		surround?
	)+
}
tuple = { (operation | value) ~ (("\t" | " ")+ ~ (operation | value))+ }
object_accessor = { var_w ~ ("." ~ symbol)+ }
object = {
	"{" ~
	(
		ws ~ symbol ~ 
		ws ~ ":" ~
		ws ~ expr ~
		nl
	)* ~
	ws ~ "}"
}
array_accessor = { var_w ~ "[" ~ ws ~ expr ~ ws ~ "]" }
array = {
	(operation | tuple | value) ~
	(
		ws ~ "," ~
		ws ~ (operation | tuple | value)
	)+
}

// Operations
op_1_symbol = { "+" | "-" }
op_1 = {
	(parens | op_2 | op_3 | !symbol ~ value) ~ ws ~
	op_1_symbol ~ ws ~
	(op_1 | parens | op_2 | op_3 | !symbol ~ value)
}
op_2_symbol = { "*" | "/" }
op_2 = {
	(parens | op_3 | !symbol ~ value) ~ ws ~
	op_2_symbol ~ ws ~
	(op_2 | parens | op_3 | !symbol ~ value)
}
op_3_symbol = { "..=" | ".." }
op_3 = {
	(parens | !symbol ~ value) ~ ws ~
	op_3_symbol ~ ws ~
	(op_3 | parens | !symbol ~ value)
}
operation = _{ op_1 | op_2 | op_3 }

// Line
var_def = { var_w ~ ws ~ "=" ~ ws ~ expr }
for_loop = { 
	"for" ~ ws ~ var_w ~ ws ~ "in" ~ ws ~ expr ~ nl ~
	PEEK_ALL ~ PUSH(indent) ~ line ~
	(PEEK_ALL ~ line)* ~
	DROP
}

// Enums
value = _{
	object_accessor | object | array_accessor | interpolation |
	dimension | symbol | hash_w | number | string_w | var_w
}
parens = _{ "(" ~ ws ~ expr ~ ws ~ ")" }
expr = _{ parens | operation | array | tuple | value }
line = { var_def | for_loop }

// Selectors
indent = _{ "\t" | "    " | "  " | " " }
sel_bit = _{ ("&" | "/" | "." | "#" | ":" | ident | ("[" ~ ANY* ~ "]"))+ }
sel = { sel_bit ~ (("\t" | " ")* ~ (sel_bit | ">"))* }
property = {
	symbol ~ ws ~
	": " ~ ws ~ expr ~
	("\t" | " ")* ~
	NEWLINE?
}
mixin_call = {
	function_w ~ ws ~
	array ~ ws ~ ")" ~ 
	("\t" | " ")* ~
	NEWLINE?
}

// Nodes
selector = {
	sel ~ (comma ~ sel)* ~ ("\t" | " ")* ~ NEWLINE ~
	(
		PEEK_ALL ~ PUSH(indent) ~ line ~ NEWLINE ~
		(PEEK_ALL ~ line ~ NEWLINE)* |
		PEEK_ALL ~ PUSH(indent) ~ (property | mixin_call | selector)
	) ~
	(nl | PEEK_ALL ~ (property | mixin_call | selector))* ~
	DROP
}
mixin = { 
	function_w ~ ws ~
	symbol ~ (comma ~ symbol)* ~ ws ~ ")" ~ NEWLINE ~
	(
		PEEK_ALL ~ PUSH(indent) ~ line ~ NEWLINE ~
		(PEEK_ALL ~ line ~ NEWLINE)* |
		PEEK_ALL ~ PUSH(indent) ~ property
	) ~
	(nl | PEEK_ALL ~ property)* ~
	DROP
}

// Root
node = _{ single_line_comment | multi_line_comment | line | selector | mixin }
file = _{
    SOI ~
    NEWLINE* ~
    (node ~ NEWLINE*)* ~
    EOI
}
